
plugins {
  id 'groovy'
  id 'eclipse'
  id 'war'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
    vendor = JvmVendorSpec.ADOPTIUM // Temurin
    implementation = JvmImplementation.VENDOR_SPECIFIC
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

version = '4.1.0-SNAPSHOT' // IMPORTANT: keep in sync with version in JexlerContextListener

//project.group = 'ch.grengine.jexler'

description =
        'Jexler is a simple relaxed Groovy framework for starting/stopping' +
        ' Groovy scripts as services and enabling them to react to events' +
        ' of their choice - great for prototyping and useful for automating.'

project.ext {
  pkg = 'war'
}

ext {
  groovy4Version='4.0.22'
  spock4Version='2.4-M4-groovy-4.0'

  groovyVersion=groovy4Version
  spockVersion=spock4Version

  //grengineVersion='3.1.0-SNAPSHOT'
  grengineVersion='3.0.2'

  ivyVersion='2.5.2'
  slf4jVersion='1.7.36'
  logbackVersion='1.3.5'
  quartzVersion='2.3.2'

  junitVersion='5.11.0'
  junitPlatformVersion='1.11.0'
}

war {
  manifest {
    attributes 'Implementation-Title': 'jexler', 'Implementation-Version': version
  }
}

dependencies {
  implementation "org.apache.groovy:groovy:$groovyVersion"
  implementation("ch.artecat.grengine:grengine:$grengineVersion") {
    exclude group: 'org.apache.groovy', module: 'groovy'
  }
  implementation "org.apache.ivy:ivy:$ivyVersion"
  implementation "org.slf4j:slf4j-api:$slf4jVersion"
  implementation "ch.qos.logback:logback-classic:$logbackVersion"
  implementation "org.quartz-scheduler:quartz:$quartzVersion"
  // for javax.activation.MimetypesFileTypeMap in Java 11
  implementation 'com.sun.activation:javax.activation:1.2.0'

  // Embedded Jetty with JSP support
  // See https://examples.javacodegeeks.com/enterprise-java/jetty/jetty-jsp-example/
  providedCompile 'org.eclipse.jetty:jetty-server:9.4.35.v20201120'
  providedCompile 'org.eclipse.jetty:jetty-webapp:9.4.35.v20201120'
  providedCompile 'org.eclipse.jetty:jetty-annotations:9.4.35.v20201120'
  providedCompile 'org.eclipse.jetty:apache-jsp:9.4.35.v20201120'

  // Unit tests
  testImplementation"org.spockframework:spock-core:$spockVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"

  testRuntimeOnly 'javax.servlet:jstl:1.2'
  testRuntimeOnly files('src/main/resources')
}

repositories {
  mavenCentral()
}

test {
  //testLogging.showStandardStreams = true
  useJUnitPlatform {
    excludeTags "slow"
    excludeTags "demo"
  }
  /*afterTest { desc, result ->
    println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
  }*/
}

task slowTests(type: Test, dependsOn: 'test') {
  testClassesDirs = testing.suites.test.sources.output.classesDirs
  classpath = testing.suites.test.sources.runtimeClasspath

  //testLogging.showStandardStreams = true
  useJUnitPlatform {
    includeTags "slow"
  }
}

task demo(type: Test) {
  testClassesDirs = testing.suites.test.sources.output.classesDirs
  classpath = testing.suites.test.sources.runtimeClasspath

  testLogging.showStandardStreams = true
  useJUnitPlatform {
    includeTags "demo"
  }
}
