subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'maven'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  version = '1.0.3-SNAPSHOT'

  project.group = 'net.jexler'

  jar {
    manifest.attributes provider: 'gradle'
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'ch.qos.logback:logback-classic:1.0.13'
    testCompile 'junit:junit:4.11'
  }

  task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
  }
  
  task jarSources(type:Jar){
    from sourceSets.main.allSource
    classifier = 'source'
  }
  
  test {
	  //testLogging.showStandardStreams = true
	  useJUnit {
        includeCategories 'net.jexler.FastTests'
    }
  }

  task slowTests(type: Test, dependsOn: 'test') {
	//testLogging.showStandardStreams = true
    useJUnit {
        includeCategories 'net.jexler.SlowTests'
    }
  }
  
  task verySlowTests(type: Test, dependsOn: 'slowTests') {
	//testLogging.showStandardStreams = true
    useJUnit {
        includeCategories 'net.jexler.VerySlowTests'
    }
  }
  
  task writePom << {
    pom {
	  project {
		name 'jexler'
		description project.description
		url 'http://www.jexler.net/'
        packaging project.ext.pkg
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
		scm {
		  url 'https://github.com/jexler/jexler'
		  connection 'https://github.com/jexler/jexler.git'
		  developerConnection 'https://github.com/jexler/jexler.git'
		}
        developers {
          developer {
            id 'jexler'
            name '$(whois jexler.net)'
            email 'jex@jexler.net'
          }
        }
	  }
    }.writeTo("$buildDir/libs/${project.name}-${project.version}.pom")
  }

}
