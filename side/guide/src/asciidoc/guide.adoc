image:http://www.jexler.net/jexler.jpg["jexler logo", link="http:www.jexler.net/"]

== jexler

Jexler is a simple relaxed Java 7 framework for starting/stopping
Groovy scripts as services and enabling them to react to events
of their choice.

== Introduction

Here's a first example of a jexler Groovy script:
[source,groovy]
----
[ "autostart" : true ]

services.add(new CronService(jexler, "every-minute").setCron("* * * * *"))
services.start()

while (true) {
  event = events.take();
  if (event instanceof CronEvent) {
    log.info("hello")
  } else if (event instanceof StopEvent) {
    return
  }
}
----

The script registers a cron service that will send it a CronEvent
every minute and in the event loop below the script waits for events
until it receives a StopEvent.

Now instead of just logging the word "hello", let's send an email instead:

[source,groovy]
----
[ "autostart" : true ]

import groovy.grape.Grape
@Grab('org.apache.commons:commons-email:1.3')
import org.apache.commons.mail.*

services.add(new CronService(jexler, "every-minute").setCron("* * * * *"))
services.start()

while (true) {
  event = events.take();
  if (event instanceof CronEvent) {
    new SimpleEmail().with {
      setFrom "jex@jexler.net"
      addTo "bugs@acme.org"
      // ...
      setSubject "hello"
      setMsg "hello from jexler script"
      send()
    }
  } else if (event instanceof StopEvent) {
    return
  }
}
----

This uses Groovy *Grape*, which allows to download external libraries
and use them immediately in the same script.

And since Groovy is syntactically a superset of Java, you can easily find
code samples and libraries for almost anything you might want to do.

Here's how the Web GUI looks like:

image:jexler-gui.jpg[web gui]

What you can see above is essentially a list of jexler Groovy scripts.
In the webapp in the file system it looks like this:

----
jexler/
  WEB-INF/
    jexlers/
      Cron.groovy
      Demo.groovy
      DirWatcher.groovy
      Util.groovy
      some.properties
      ...
    ...
  ...
----

The first three jexlers are running, the last one is off, and you could start
or stop them in the GUI, or look at the log file, edit the scripts, etc.

If you want to try it out and play with jexler immediately:

* Get the jexler source from github: https://github.com/jexler/jexler
* Install the Java 7 JDK and Gradle
* `gradle demo`
* Open http://localhost:9080/ in a web browser
* See the README at github for alternatives...

== Services

=== CronService

This service sends a CronEvent at times configurable with a cron string:

[source,groovy]
services.add(new CronService(jexler, "hourly").setCron("0 * * * *"))

Note that the `setCron()` method returns its CronService instance, so that setters can be chained.

There are two special cron strings that may be useful for testing:

* "now": Sends a single CronEvent immediately.
* "now+stop": Sends a single CronEvent immediately, followed by a single StopEvent.

The CronEvent class has a single getter `getCron()` to get the cron string that caused the event:

[source,groovy]
log.trace(cronEvent.cron)
    
(Note that `cronEvent.cron` is a Groovy shortcut for `cronEvent.getCron()`.)

Implemented using the Open Source http://www.sauronsoftware.it/projects/cron4j/[cron4j] library.

=== DirWatchService

This service observes a directory for changes in the file system and sends events
when a file is created, modified or deleted:

[source,groovy]
----
services.add(new DirWatchService(jexler, "watch-jexler-dir"))
services.start()

while (true) {
  event = events.take();
  if (event instanceof DirWatchEvent) {
    log.trace("Got file change: ${event.kind} '${event.file.name}'")
  } else if (event instanceof StopEvent) {
    return
  }
}
----

There are two setters:

* `setDir(File dir)`: The directory to watch,
  default if not set is the directory that contains the jexler.
* `setSleepTimeMs(long sleepTimeMs)`:
   Time to sleep between polling file system,
   default if not set is 1000ms (1 sec).

The DirWatchEvent class has the following getters:

* `File getFile()`: Get file that has been created, modified or deleted.
* `WatchEvent.Kind<?> getKind()`: Get what happened with the file,
   can be StandardWatchEventKinds.ENTRY_CREATE,
   .ENTRY_MODIFY or .ENTRY_DELETE.

Implemented using a Java 7 WatchService.

=== More Services

Writing your own services is relatively easy, since you can also write services
in Groovy, even from within the jexler web GUI.

The trick is that all Groovy scripts in the jexlers directory are part of the class path.

So, for example, if you wanted a more sophisticated version of CronService, you could
copy the CronService.java from the jexler source to a MyCronService.groovy in the
jexlers directory in the jexler webapp and do the same for CronEvent.
After a few boilerplate changes, you should have a MyCronService that does the same
as CronService (this works, I tried it!) and then you could start adding new features, etc.

And if you feel that it would be great if jexler had more services out-of-the-box,
feel free to write your own Java or Groovy library of services and make it available.

Side remark: If you wanted an additional service to be included with jexler itself,
it would have to be something really, really, really central and generally useful and simple
to manage and test, otherwise I wouldn't touch it ;)

And even then...

== Tools

=== ShellTool

This tool helps to run shell commands. (Note that there are already at least
two standard ways of doing this with Groovy APIs, which may or may not be
more convenient depending on your use case.)

[source,groovy]
shellTool = new ShellTool()
result = shellTool.run("echo 'hello world'")
log.trace(result.toString())

There are the following setters:

* `setWorkingDirectory(File dir)`:
   Set working directory for the command;
   if not set or set to null, inherit from parent process.
* `setEnvironment(Map<String,String> env)`:
  Set environment variables for the command
  (key is variable name, value is variable value);
  if not set or set to null, inherit from parent process.
* `setStdoutLineHandler(Closure<?> handler)`:
  Set a closure that will be called to handle each line of stdout;
  if not set or set to null, do nothing.
* `setStderrLineHandler(Closure<?> handler)`:
  Set a closure that will be called to handle each line of stderr;
  if not set or set to null, do nothing.

Note that the setters again return their ShellTool instance, i.e. setters can be chained:

[source,groovy]
result = new ShellTool().setWorkingDirectory('/tmp').setStderrLineHandler({log.info(it)}).run('ls')

And there are two methods for running a shell command:

* `Result run(String command)`
* `Result run(List<String> cmdList)`

The second method allows to explicitly indicate the application to run
(first list element) and how to split its arguments.

Passing the right command string can be a bit tricky:

* On windows some common shell commands like "dir" or "echo" are not actually commands,
  but arguments to cmd.exe, so use e.g. `cmd /c echo hello` as a command string.
* To set the working directory for cygwin, use e.g. `c:/cygwin/bin/bash -l /my/working/dir ls -l`.
* Sometimes there is no way around splitting up arguments explicitly, a single string won't do.

The Result contains three items:

* `int rc`: The return code of the command (0 is no error, other values indicate an error).
* `String stdout`: The output of the command.
* `String stderr`: The error output of the command.

If an exception occurs, the return code of the result is set to -1,
stderr of the result is set to the stack trace of the exception and stdout
of the result is set to an empty string.

Note that the `toString()` method of Result produces a single line string suitable
for logging. Line breaks in stdout and stderr are replaced by '%n'.

Implemented using `Runtime.getRuntime().exec()`.

=== StringObfuscatorTool

This tool can help to obfuscate passwords and other sensitive strings.
By default, it uses 128 bit AES with a hard-coded key, see below plus code/javadoc for full details.

* `String obfuscate(String plain)`:
  UTF-8 encode, pad with random bytes, encipher and hex encode given string.
* `public String deobfuscate(String encHex)`:
  Hex decode, decipher, unpad and UTF-8 decode given string.
* `StringObfuscatorTool()`: Default constructor.
   Chooses 128 bit AES (AES/CBC/PKCS5Padding) with a hard-coded default key and iv,
   and sets byteBufferPadLen to 64, which limits plain strings to max 47 characters
   (resp. less if some plain string characters need more than one byte UTF-8 encoded).
* `StringObfuscatorTool setParameters(String hexKey, String hexIv, String algorithm, String transformation)`:
  Set key, iv, algorithm and transformation.
* `StringObfuscatorTool setByteBufferPadLen(int len)`:
  Set the length to which to pad the plain string as UTF-8 encoded byte buffer.

Simple use case:

* Log obfuscated password:
  `log.trace(new StringObfuscatorTool().obfuscate("mysecret"))`
* Copy obfuscated password from log file (and delete entry from log file).
* Use it: `def password = new StringObfuscatorTool().deobfuscate("2A8A0F ... 5DA963")`

Note that this is overall not a cryptographically strong protection of secrets,
just a countermeasure to fend off the simplest attacks, like e.g. "shoulder surfing".
Someone with access to the running jexler with write permission for jexler scripts
can easily deobfuscate secrets. Someone with only read access to jexler scripts
can also simply copy the obfuscated string and deobfuscate it on a different
jexler instance. To fend off that attack, e.g. store obfuscated passwords in files
in the jexlers directory:

[source,groovy]
new File("password.txt").setText(new StringObfuscatorTool().obfuscate("mysecret"))
def password = new StringObfuscatorTool().deobfuscate(new File("password.txt").text)

To obfuscate things even a little more, you could set custom cipher parameters
that you would read from a file, or maybe even consider something like the following.
Subclass the StringObfuscatorTool class in Groovy (or Java):

[source,groovy]
class MyObfuscatorTool extends net.jexler.tool.StringObfuscatorTool {
  public MyObfuscatorTool() {
    setParameters("00--my-AES-128-secret-key-hex-00", "00--my-AES-128-secret-iv-hex--00",
      "AES", "AES/CBC/PKCS5Padding")
  }
}

Compile the class and place the resulting class file in the jexlers directory
or within the WEB-INF/lib directory, i.e. add it to the classpath of the running jexlers.
This would make it a little harder to deobfuscate strings even to someone with
read access to the files in the jexlers resp. WEB-INF/lib directory, because the keys
are somewhat "hidden" in the class file.

=== More Tools

With Java and Groovy plus Grape you have ***thousands*** of tools and libraries
at your fingertips, just search the internet when you need something specific.

Note again that since almost all Java code is valid Groovy code, you can search
for solutions in Java and Groovy to find something you can use in jexler scripts.

Besides, essentially the same comments as for services apply also to tools.
No need to reinvent the wheel.

== Web GUI

=== Basic Usage

image:jexler-gui-basic.jpg[web gui basic usage]

Use the red/green/blue buttons in the first two columns of the table to start/stop/restart
a single jexler or all jexlers (top row).

Note that a jexler utility Groovy script that just declares a class with methods
simply runs and stops immediately again (since its `main()` method is implicitly empty),
so this causes no trouble at all when starting/stopping all jexlers.

The third column allows to view the jexler log file (blue button in top row) and
to view any issues that a jexler may have had, where a green button means that
there are no issues and a red button can be clicked to view the issue(s).

*Issues* are what jexler usually creates when something exceptionally happens that might
require intervention by an administrator to get things running smoothly again.

Jexler uses http://logback.qos.ch[logback] for logging, by default
(see WEB-INF/classes/logback.xml) the jexler webapp logs to `${catalina.base}/logs/jexler.log`
(with daily log rotation). If you change that location, the GUI should still automatically
find the log file, unless you do something more fancy, like splitting up logging into several files.

Click the name of any jexler in the fourth column to edit its script.
Hover over the name to see the run state of the jexler.

There are five run states that apply to a jexler:

* *off*: Not running.
* *busy (starting)*: Busy starting, not ready to process events, yet.
* *idle*: Waiting for an event, i.e. hanging in `event.take()`.
* *busy (event)*: Busy processing an event.
* *busy (stopping)*: Stopping, not processing events any more.

These run states also apply to all jexlers as a group (and technically even to services,
like the CronService).

Click the jexler logo to reload the main view. Note that the table with the run states
is reloaded automatically every second by JavaScript. You typically only need to reload
explicitly if JavaScript is off or for older Internet Explorer browsers for which
this feature has not been supported in the jexler web GUI.

Finally, hover over the jexler logo to see the jexler release version.

=== Edit jexler Scripts

image:jexler-gui-edit.jpg[web gui edit scripts]

New files are created simply by typing a new name and clicking save.

Note that save does by default not ask for permission before (over-)writing a script,
whereas delete asks by default for permission. These settings can be changed in the web.xml,
see further below.

=== Issues

image:jexler-gui-issues.jpg[web gui view issues]

Issues are automatically created if a jexler unexpectedly exits by throwing an exception.

Often it is better to catch exceptions within the jexler script to keep the jexler running,
and instead to track the exception as a issue in the script:

[source,groovy]
try {
  new SimpleEmail().with {
    addTo to
    //...
    send()
  }
  log.trace("mail successfully sent to $to")
} catch (EmailException e) {
  jexler.trackIssue(jexler, "Could not send mail to $to.", e)
  return false
}

Parameters are:

* `Service service`: The service where the issue occurred, may be null.
* `String message`: A message that provides information about the issue.
* `Exception exception`: The exception (if any) that caused the issue, may be null.

Tracked issues are always additionally logged with level error (as a single line,
with full stack trace, if available, and with linebreaks translated to '%n').

=== View Log

image:jexler-gui-log.jpg[web gui view log file]

Note that newest log entries are on top.

=== Customizing (and Security)

Several context parameters can be set in the web.xml.

[source,xml]
<context-param>
  <description>Timeout for starting a jexler in ms.</description>
  <param-name>jexler.start.timeout</param-name>
  <param-value>10000</param-value>
</context-param>
<context-param>
  <description>Timeout for stopping a jexler in ms.</description>
  <param-name>jexler.stop.timeout</param-name>
  <param-value>10000</param-value>
</context-param>

These two parameters control how long the jexler waits before returning
to the client when starting / stopping a jexler or all jexlers.
(An issue is tracked if the timeout occurs.) Default is 10 sec each.

[source,xml]
<context-param>
  <description>Whether to allow editing jexler scripts in web gui or not.</description>
  <param-name>jexler.security.script.allowEdit</param-name>
  <param-value>true</param-value>
</context-param>

This parameter can be used to disallow editing of jexler scripts in the GUI as a security measure. Default is to allow editing.

Please be aware that jexler (thanks to Groovy and Grape) is a very powerful tool:

*_Giving someone access to a jexler web GUI with write permission for scripts
is practically like giving someone shell access as the user under which
the web GUI is running_*.

So, please protect the web GUI accordingly.

Without write permission, jexler is relatively harmless, also since it is not possible
to give a jexler any kind of start parameters in the web GUI without editing the script.

[source,xml]
<context-param>
  <description>Whether to confirm script save in web gui or not.</description>
  <param-name>jexler.safety.script.confirmSave</param-name>
  <param-value>false</param-value>
</context-param>
<context-param>
  <description>Whether to confirm script delete in web gui or not.</description>
  <param-name>jexler.safety.script.confirmDelete</param-name>
  <param-value>true</param-value>
</context-param>

These two parameters indicate whether the web GUI should ask the user to confirm
before saving or deleting a jexler script file. Default is false for saving and true for deleting.

== Troubleshooting

=== Grape Concurrency Issues

There is a bug in Groovy/Grape and Ivy up to at least version 2.4.3 resp. 2.4.0.
Using Grape is simply not thread-safe (unless only a single GroovyClassLoader
is used), see https://issues.apache.org/jira/browse/GROOVY-7407

As a workaround, there are two system properties that can be set:

* `net.jexler.workaround.groovy.7407.retries`:
  If set to a number, compilation of each jexler is retried for the indicated
  number of times.
* `net.jexler.workaround.groovy.7407.retries.report`:
  If set to "true", successful compilation retries report a jexlers issue.
  (If compilation fails at all retries, a jexler issue is always reported.)

This is only a partial workaround, but it reduces the chance for startup failures,
when several scripts are (auto)started in parallel.

=== Tomcat Startup Issues

Most issues if jexlers do not autostart seem related to the issue above.

But there is another less frequent issue that I observed (CentOS / Tomcat 7),
namely that sometimes a jexler is started, i.e. the script is running,
but not shown as running, maybe because a (single) cron service that was
nominally started does not report back its status as no longer `OFF`.

== Source Code

The source code is at github: https://github.com/jexler/jexler

See there for instructions how to build.

The code is a Gradle / Eclipse project that contains two Java sub-projects:

* *jexler-core*: The core jexler library (JAR) which contains also all services and tools.
* *jexler*: The jexler web GUI, a simple Java webapp (WAR) with a single JSP.

Java 7 is required.

The jexler-core is deeply tested, close to 100% test coverage in jacoco, except for a few artefacts.

The jexler webapp is very simple and contains a demo unit test that starts it in a Jetty embedded web server.

All interfaces and classes in jexler-core that are not in packages that end with ".internal"
are basically public and should normally remain backwards compatible as long as the jexler
major version does not change (i.e. jexler 4.5.6 would be backwards compatible with 4.0.0,
but 5.0.0 would not; this numbering scheme is common practice). Of course, if only
very few people use specific feature or if some new feature reveals itself to be
really badly designed, exceptions may be made in order to make life easier for most users.

Within a jexler, the following packages are automatically imported by default:

* net.jexler
* net.jexler.service
* net.jexler.tool

and the following variables are available to jexler Groovy scripts:

* `Jexler jexler`: The jexler instance.
* `Jexlers jexlers`: The jexlers instance, i.e. the class
   that abstracts all jexlers in a directory.
* `List<Event> events`: The list of events to poll for new events.
* `ServiceGroup services`: The group of services to add services to and to start then.
   It is not mandatory to add any services here, they can also be managed separately,
   but often it is convenient that services added to this service group are automatically
   stopped if the jexler exits (regularly or due to an exception).
* `Logger log`: The logback logger for the jexler instance.

=== Meta Info

The first line of a jexler script may contain a map with string keys and values
of any value, the so called *meta info*, e.g.:

[source,groovy]
----
[ 'autostart' : true, 'autoimport' : true, 'whatever' : 'my words' ]
----

These are evaluated before running the jexler script, i.e. none of the variables
listed above are available for that map.
There are two boolean settings that jexler uses by default:

* `autostart`: If true, the jexler is started when the webapp starts up.
   Default is false.
* `autoimport`: If false, the three mentioned packages are not automatically imported. Default is true.

=== Distribution

* jexler-core is at http://search.maven.org/#search%7Cga%7C1%7Cjexler-core[Maven Central]
* jexler webapp is at https://sourceforge.net/projects/jexler/[Sourceforge] (see "Files" tab)
* http://www.jexler.net/[jexler.net] hosts http://www.jexler.net/javadoc/[Javadoc],
  http://www.jexler.net/jacoco/[jacoco] and this http://www.jexler.net/guide/[guide]

== Use Cases

=== Automatic Builds (jexler itself and httest Binaries)

In 2013, I have used jexler to make nightly builds of jexler on four different
platforms: Mac, Windows and Debian Linux 32 bit and 64 bit. This included roughly
checking out the source from git, running the build and sending a mail with
the result if not OK. On each of the four platforms, there was an independent
Tomcat with its jexler webapp (and jexlers shared via a "private" git repository
at sourceforge).

On the same four platforms, I also made nightly and release builds of **httest**,
an Open Source HTTP test tool written in C by Christian Liesch
(and with some minor contributions by me and others):

* "httest is a script based tool for testing and benchmarking web applications,
web servers, proxy servers and web browsers. httest can emulate clients and servers
in the same test script, very useful for testing proxys."
* Project: http://htt.sourceforge.net/
* Sourceforge: http://sourceforge.net/projects/htt/
* Binaries (by me): http://www.jexler.net/htt/

Release builds were triggered automatically by polling sourceforge for a new httest
source release. After checking out the source and building the binaries, the binaries
were uploaded via FTP to www.jexler.net and notification mails sent, as needed.
The builds (nightly and release) included also to run all tests automatically and
provide a test report. The Windows build resulted also in a Visual Studio Solution,
complete with all needed external libraries and includes. For building on Windows,
a combination of cygwin and Visual Studio was used, so that most parts of the build
could be shared as bash scripts across all four platforms, but that's already
a different story...

=== Checks and Cleanups

At work I continue to use it for various minor maintenance things, e.g. for checking
if certain Hudson nightly builds have really run or for warning if disk space is
getting low resp. cleaning up right away in that case, and for a few more things.

=== More

I am curious whether and for what purposes jexler might be used, but would also not be angry
if practically nobody uses it, it was fun to write jexler and I personally like it, both
from a technical and an artistic perspective - that's reward enough for me :)

Jexler is maybe more suited for tasks that have some leisure in them, but in principle
you could also imagine to write a web server with jexlers as handlers or similar things.

Keep me updated at mailto:jex@jexler.net[jex@jexler.net].
(Please be patient, I might not read mails to that address regularly.)

== Roadmap

Well, there is none, except to keep jexler really small and to keep the quality high. :)

I will gladly link third party libraries and similar additions around jexler
on my web site and, if you want to take it all to a new level, feel free to
do so within jexler's Apache 2 Open Source license.

Or to put it a bit more poetically, to me jexler is an island,
things may flow freely around it, but jexler itself is unlikely
to change much.

Copyright &copy; 2012-now $(whois jexler.net) +
http://www.jexler.net/

Note also that the jexler logo with the bat is my own creation (*2010).

== License

Licensed under the Apache License, Version 2.0 (the "License"); +
you may not use this file except in compliance with the License. +
You may obtain a copy of the License at +

http://www.apache.org/licenses/LICENSE-2.0 +

Unless required by applicable law or agreed to in writing, software +
distributed under the License is distributed on an "AS IS" BASIS, +
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. +
See the License for the specific language governing permissions and +
limitations under the License.